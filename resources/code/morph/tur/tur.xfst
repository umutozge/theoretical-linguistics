!
! COGS 532 Theoretical Linguistics
!
! Umut Özge
! METU Informatics
!

clear

! Sound abstractions
! A : e a
! H : ı i u ü
! I : ı i
! N : n 0
! Y : y 0
! S : s 0
! D : d t


! Consonants
define C  b|c|ç|d|f|g|ğ|h|j|k|l|m|n|p|r|s|ş|t|v|y|z;

! Vowels
define V  a|e|i|o|u|ü|ö|ı;

! Orthographic symbols -- all symbols that can appear in words; we omit capitalization.
define O  [C | V];

! Vowel abstractions
define VA H|I|A;

! Consonant abstractions
define CA S|D|Y|N;

! Consonant features -- only the relevant ones
define -VOICE p|ç|t|k|f|s|ş|h;
define +VOICE C - %-VOICE;

! Vowel features
define +HIGH i|u|ı|ü;
define -HIGH e|a|o|ö;
define +BACK ı|a|o|u;
define -BACK i|e|ö|ü;
define +ROUND ö|ü|o|u;
define -ROUND e|a|i|ı;

define ConsAlter 	S -> s || V %^ _ ,,
			S -> 0 || \V %^ _ ,,
			N -> 0 || _ .#. ,,
			N -> n || _ ? ,,
		 	Y -> 0 || ~[V|VA] _ ,,
			Y -> y || [V|VA] _ ,,
			D -> t || %-VOICE %^ _ ,,
			D -> d || \%-VOICE %^ _ ,, 
			b -> p, d -> t, ğ -> k, c -> ç || _ [.#. | [ %^ [C|D] ]];


define ARule  	A -> %-HIGH & %-ROUND & %+BACK || %+BACK \V* _ ,,
		A -> %-HIGH & %-ROUND & %-BACK || %-BACK \V* _ ;

define HRule  	H -> %+HIGH & %+ROUND & %+BACK || [%+BACK & %+ROUND] \V* _ ,,
         	H -> %+HIGH & %+ROUND & %-BACK || [%-BACK & %+ROUND] \V* _ ,,
          	H -> %+HIGH & %-ROUND & %+BACK || [%+BACK & %-ROUND] \V* _ ,,
          	H -> %+HIGH & %-ROUND & %-BACK || [%-BACK & %-ROUND] \V* _ ;

define RemMorphBound	%^ -> 0;

push RemMorphBound
push ConsAlter
push HRule
push ARule
read lexc tur.lexc
compose net
